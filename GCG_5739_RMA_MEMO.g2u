Program.Sub.ScreenSU.Start
Gui.fmMaster..Create
Gui.fmMaster..Caption("RMA Header Memo")
Gui.fmMaster..Size(14625,4770)
Gui.fmMaster..MinX(0)
Gui.fmMaster..MinY(0)
Gui.fmMaster..Position(0,0)
Gui.fmMaster..BackColor(-2147483633)
Gui.fmMaster..MousePointer(0)
Gui.fmMaster..Event(UnLoad,fmMaster_UnLoad)
Gui.fmMaster.lblRMAlbl.Create(Label,"RMA:",True,570,255,0,255,135,True,0,"Arial",8,-2147483633,0)
Gui.fmMaster.lblRMAlbl.FontStyle(True,False,False,False,False)
Gui.fmMaster.lblRMAtxt.Create(Label,"",True,1935,255,0,900,135,False,0,"Arial",8,16777215,0)
Gui.fmMaster.lblCustIDlbl.Create(Label,"Customer ID:",True,1245,255,0,2835,135,True,0,"Arial",8,-2147483633,0)
Gui.fmMaster.lblCustIDlbl.FontStyle(True,False,False,False,False)
Gui.fmMaster.lblCustIDtxt.Create(Label,"",True,1935,255,0,4125,135,False,0,"Arial",8,16777215,0)
Gui.fmMaster.lblCustNamelbl.Create(Label,"Customer Name:",True,1545,255,0,6060,135,True,0,"Arial",8,-2147483633,0)
Gui.fmMaster.lblCustNamelbl.FontStyle(True,False,False,False,False)
Gui.fmMaster.lblCustNametxt.Create(Label,"",True,1935,255,0,7620,135,False,0,"Arial",8,16777215,0)
Gui.fmMaster.GsMemo.Create(GsGridControl)
Gui.fmMaster.GsMemo.Size(14340,3780)
Gui.fmMaster.GsMemo.Position(45,420)
Gui.fmMaster.GsMemo.Event(RowCellClick,GsMemo_RowCellClick)
Gui.fmMaster.cmdAdd.Create(Button)
Gui.fmMaster.cmdAdd.Size(2370,375)
Gui.fmMaster.cmdAdd.Position(12015,30)
Gui.fmMaster.cmdAdd.Caption("Add Memo")
Gui.fmMaster.cmdAdd.Event(Click,cmdAdd_Click)
Gui.fmMaster.cmdAdd.FontStyle(True,False,False,False,False)
Gui.fmMX..Create
Gui.fmMX..Caption("Add/Delete RMA Memo")
Gui.fmMX..Size(8925,3135)
Gui.fmMX..MinX(0)
Gui.fmMX..MinY(0)
Gui.fmMX..Position(0,0)
Gui.fmMX..BackColor(-2147483633)
Gui.fmMX..MousePointer(0)
Gui.fmMX..Event(UnLoad,fmMX_UnLoad)
Gui.fmMX.lblRMAlbl.Create(Label,"RMA:",True,585,255,0,165,225,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblRMAlbl.FontStyle(True,False,False,False,False)
Gui.fmMX.lblRMAtxt.Create(Label,"",True,1935,255,0,750,225,True,0,"Arial",8,16777215,0)
Gui.fmMX.lblCustIDlbl.Create(Label,"Customer ID:",True,1185,255,0,2760,225,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblCustIDlbl.FontStyle(True,False,False,False,False)
Gui.fmMX.lblCustIDtxt.Create(Label,"",True,1290,255,0,3960,225,True,0,"Arial",8,16777215,0)
Gui.fmMX.lblCustNamelbl.Create(Label,"Customer Name:",True,1500,255,0,5340,225,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblCustNamelbl.FontStyle(True,False,False,False,False)
Gui.fmMX.lblCustNamettxt.Create(Label,"",True,1800,255,0,6825,225,True,0,"Arial",8,16777215,0)
Gui.fmMX.lblLinelbl.Create(Label,"Memo Line:",True,1155,255,0,165,540,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblLinelbl.FontStyle(True,False,False,False,False)
Gui.fmMX.lblLinetxt.Create(Label,"",True,1290,255,0,1380,540,True,0,"Arial",8,16777215,0)
Gui.fmMX.lblDatelbl.Create(Label,"Date:",True,615,255,0,2760,540,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblDatelbl.FontStyle(True,False,False,False,False)
Gui.fmMX.lblDatetxt.Create(Label,"",True,1860,255,0,3390,540,True,0,"Arial",8,16777215,0)
Gui.fmMX.lblUserlbl.Create(Label,"Entered By:",True,1035,255,0,5340,540,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblUserlbl.FontStyle(True,False,False,False,False)
Gui.fmMX.lblUsertxt.Create(Label,"",True,1800,255,0,6825,540,True,0,"Arial",8,16777215,0)
Gui.fmMX.txtMemo.Create(TextboxM)
Gui.fmMX.txtMemo.Size(8340,855)
Gui.fmMX.txtMemo.Position(165,1110)
Gui.fmMX.lblMemolbl.Create(Label,"Memo:",True,1935,255,0,165,870,True,0,"Arial",8,-2147483633,0)
Gui.fmMX.lblMemolbl.FontStyle(True,False,False,False,False)
Gui.fmMX.cmdSv.Create(Button)
Gui.fmMX.cmdSv.Size(855,375)
Gui.fmMX.cmdSv.Position(6690,2070)
Gui.fmMX.cmdSv.Caption("Save")
Gui.fmMX.cmdSv.Event(Click,cmdSv_Click)
Gui.fmMX.cmdSv.FontStyle(True,False,False,False,False)
Gui.fmMX.cmdDel.Create(Button)
Gui.fmMX.cmdDel.Size(855,375)
Gui.fmMX.cmdDel.Position(7650,2070)
Gui.fmMX.cmdDel.Caption("Delete")
Gui.fmMX.cmdDel.Event(Click,cmdDel_Click)
Gui.fmMX.cmdDel.FontStyle(True,False,False,False,False)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'TJS, October 2019
'Customer: Controlled Fluids, Inc.
'Customer needs to add memo notes field to the script 1 in the RMA input screen. This memo will not print on the RMA report.
'Hooks in play: 35601 (RMA Populate), 35611 (RMA Script 1)
'Passed values: 000001 (RMA NO), 000124 (RMA Script 1 Button), 000043 (Customer ID), 000044 (Customer Name)

F.Intrinsic.Control.Try

F.Intrinsic.Control.SelectCase(V.Caller.Hook)
	F.Intrinsic.Control.Case(35601)
		V.Passed.000124.Set("RMA MEMO")
		F.Intrinsic.Control.End
	F.Intrinsic.Control.Case(35611)
		F.Intrinsic.Control.CallSub(Data)
	F.Intrinsic.Control.CaseElse
		F.Intrinsic.Control.End
F.Intrinsic.Control.EndSelect

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Data.Start
F.Intrinsic.Control.Try
V.Local.sSQL.Declare
V.Local.sSQL1.Declare
V.Local.sRET.Declare

'Collect initial data
F.ODBC.Connection!conts.OpenCompanyConnection

F.Intrinsic.String.Build("select * from GCG_5739_RMA_MEMO where RMA_NO = '{0}' order by MEMO_LINE desc",V.Passed.000001,V.Local.sSQL)

F.ODBC.Connection!conts.ExecuteAndReturn(V.Local.sSQL,"conts",V.Local.sRET)

F.Data.DataTable.CreateFromSQL("dtMemo","conts",V.Local.sSQL,True)

F.ODBC.Connection!conts.Close

F.Intrinsic.Control.CallSub(Grid)

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.Data.End

Program.Sub.Grid.Start
F.Intrinsic.Control.Try

F.Intrinsic.Control.CallSub(SetIcon)

'Set the uneditable controls on the form
Gui.fmMaster.lblRMAtxt.Caption(V.Passed.000001.Trim)
Gui.fmMaster.lblCustIDtxt.Caption(V.Passed.000043.Trim)
Gui.fmMaster.lblCustNametxt.Caption(V.Passed.000044.Trim)

Gui.fmMaster.GsMemo.SuspendLayout

Gui.fmMaster.GsMemo.AddGridviewFromDatatable("gvMemo","dtMemo")
Gui.fmMaster.GsMemo.MainView("gvMemo")

Gui.fmMaster.GsMemo.SetGridviewProperty("gvMemo","Editable",True)

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","RMA_NO","Visible",False)

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_LINE","Caption","Memo Line")
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_DATE","Caption","Date")
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO","Caption","Memo")
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","USER_FLD","Caption","Entered By")

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_LINE","HeaderFontBold",True)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_DATE","HeaderFontBold",True)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO","HeaderFontBold",True)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","USER_FLD","HeaderFontBold",True)

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_LINE","ReadOnly",True)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_DATE","ReadOnly",True)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO","ReadOnly",True)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","USER_FLD","ReadOnly",True)

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_LINE","AllowEdit",False)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_DATE","AllowEdit",False)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO","AllowEdit",False)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","USER_FLD","AllowEdit",False)

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_LINE","Width",40)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO","Width",300)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_DATE","Width",50)
Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","USER_FLD","Width",50)

Gui.fmMaster.GsMemo.SetColumnProperty("gvMemo","MEMO_DATE","DisplayCustomDateTime",d)

Gui.fmMaster.GsMemo.ResumeLayout

Gui.fmMaster..Show

F.Data.DataTable.Close("dtMemo")

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.Grid.End

Program.Sub.GsMemo_RowCellClick.Start
F.Intrinsic.Control.Try
V.Local.sSQL.Declare
V.Local.sData.Declare
V.Local.dDate.Declare

F.Intrinsic.Control.If(V.Args.Column,=,"MEMO_LINE")
	F.ODBC.Connection!conts.OpenCompanyConnection
	F.Intrinsic.String.Build("select * from GCG_5739_RMA_MEMO where RMA_NO = '{0}' and MEMO_LINE = '{1}' ",V.Passed.000001.Trim,V.Args.CellValue,V.Local.sSQL)
	F.Data.DataTable.CreateFromSQL("dtTemp","conts",V.Local.sSQL)
	F.Data.DataView.Create("dtTemp","dvTemp",22)
	F.Data.DataView.ToString("dtTemp","dvTemp","RMA_NO*!*MEMO_LINE*!*MEMO_DATE*!*MEMO*!*USER_FLD","*!*","",V.Local.sData)
	F.Intrinsic.String.Split(V.Local.sData,"*!*",V.Local.sData)
	
	V.Local.dDate.Set(V.Local.sData(2))
	Gui.fmMX.lblRMAtxt.Caption(V.Passed.000001.Trim)
	Gui.fmMX.lblCustIDtxt.Caption(V.Passed.000043.Trim)
	Gui.fmMX.lblCustNamettxt.Caption(V.Passed.000044.Trim)
	Gui.fmMX.lblUsertxt.Caption(V.Local.sData(4))
	Gui.fmMX.lblLinetxt.Caption(V.Local.sData(1))
	Gui.fmMX.txtMemo.Text(V.Local.sData(3))
	Gui.fmMX.lblDatetxt.Caption(V.Local.dDate.Trim)
	F.ODBC.Connection!conts.Close
	F.Data.DataView.Close("dtTemp","dvTemp")
	F.Data.DataTable.Close("dtTemp")
	Gui.fmMX..Show
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.GsMemo_RowCellClick.End


Program.Sub.cmdAdd_Click.Start
F.Intrinsic.Control.Try
V.Local.dDate.Declare

V.Local.dDate.Set(V.Ambient.Date.Trim)

Gui.fmMX.lblRMAtxt.Caption(V.Passed.000001.Trim)
Gui.fmMX.lblCustIDtxt.Caption(V.Passed.000043.Trim)
Gui.fmMX.lblCustNamettxt.Caption(V.Passed.000044.Trim)
Gui.fmMX.lblDatetxt.Caption(V.Local.dDate)
Gui.fmMX.lblUsertxt.Caption(V.Caller.User.Trim)
Gui.fmMX.txtMemo.Caption("")
Gui.fmMX.lblLinetxt.Caption("")

Gui.fmMX..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.cmdAdd_Click.End

Program.Sub.cmdSv_Click.Start
F.Intrinsic.Control.Try
V.Local.sSQL.Declare
V.Local.sLine.Declare

F.ODBC.Connection!conts.OpenCompanyConnection

F.Intrinsic.String.Build("select Top 1 MEMO_LINE from GCG_5739_RMA_MEMO order by MEMO_LINE desc",,V.Local.sLine)
F.ODBC.Connection!conts.ExecuteAndReturn(V.Local.sLine,V.Local.sLine)

F.Intrinsic.Control.If(V.Ambient.ExecuteAndReturnEOF)
	V.Local.sLine.Set("0")
F.Intrinsic.Control.ElseIf(V.Local.sLine,=,V.Screen.fmMX!lblLinetxt.Caption)
	V.Local.sLine.Set(V.Screen.fmMX!lblLinetxt.Caption)
F.Intrinsic.Control.Else
	F.Intrinsic.Math.Add(V.Local.sLine,1,V.Local.sLine)
F.Intrinsic.Control.EndIf

F.Intrinsic.String.Build("{0}{1}{2}{1}{3}{1}{4}{1}{5}",V.Screen.fmMX!lblRMAtxt.Caption,"*!*",V.Local.sLine,V.Screen.fmMX!lblDatetxt.Caption,V.Screen.fmMX!txtMemo.Caption,V.Screen.fmMX!lblUsertxt.Caption,V.Local.sSQL)

F.Data.DataTable.CreateFromString("dtSave",V.Local.sSQL,"RMA_NO*!*MEMO_LINE*!*MEMO_DATE*!*MEMO*!*USER_FLD","STRING*!*STRING*!*DATE*!*STRING*!*STRING","*!*","")
F.Data.DataTable.SaveToDB("dtSave","conts","GCG_5739_RMA_MEMO","RMA_NO*!*MEMO_LINE",256)

F.ODBC.Connection!conts.Close

F.Data.DataTable.Close("dtSave")

F.Intrinsic.Control.CallSub(fmMX_UnLoad)
F.Intrinsic.Control.CallSub(Data)

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.cmdSv_Click.End

Program.Sub.cmdDel_Click.Start
F.Intrinsic.Control.Try
V.Local.sSQL.Declare

F.ODBC.Connection!conts.OpenCompanyConnection

F.Intrinsic.String.Build("delete GCG_5739_RMA_MEMO where RMA_NO = '{0}' and MEMO_LINE = '{1}'",V.Screen.fmMX!lblRMAtxt.Caption,V.Screen.fmMX!lblLinetxt.Caption,V.Local.sSQL)
F.ODBC.Connection!conts.Execute(V.Local.sSQL)

F.ODBC.Connection!conts.Close

F.Intrinsic.Control.CallSub(fmMX_UnLoad)
F.Intrinsic.Control.CallSub(Data)

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.cmdDel_Click.End

Program.Sub.SetIcon.Start
F.Intrinsic.Control.Try
V.Local.sForm.Declare
V.Local.bExists.Declare
V.Local.sIcon.Declare
V.Local.iCounter.Declare

F.Intrinsic.String.Build("{0}\ART\gss2.ico",V.Caller.GlobalDir,v.Local.sIcon)
F.Intrinsic.File.Exists(V.Local.sIcon, V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists.Not)
    F.Intrinsic.String.Build("{0}\GAB\GAS\gss2.ico",V.Caller.PluginsDir,v.Local.sIcon)
    F.Intrinsic.File.Exists(V.Local.sIcon, V.Local.bExists)
    F.Intrinsic.Control.If(V.Local.bExists.Not)
        F.Intrinsic.Control.ExitSub
    F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf

F.Intrinsic.UI.GetFormList(V.Local.sForm)
F.Intrinsic.String.Split(V.Local.sForm, "*!*", V.Local.sForm)

F.Intrinsic.Control.For(V.Local.iCounter, 0, V.Local.sForm.UBound, 1)
    Gui.[V.Local.sForm(V.Local.iCounter)]..Icon(V.Local.sIcon)
F.Intrinsic.Control.Next(V.Local.iCounter)

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.CallSub(Error,"SubRoutine",V.Ambient.CurrentSubroutine,"eNumber",V.Ambient.ErrorNumber,"eDescription",V.Ambient.ErrorDescription)
F.Intrinsic.Control.EndTry
Program.Sub.SetIcon.End

Program.Sub.fmMX_UnLoad.Start
Gui.fmMX..Visible(False)
Program.Sub.fmMX_UnLoad.End

Program.Sub.fmMaster_UnLoad.Start
F.Intrinsic.Control.End
Program.Sub.fmMaster_UnLoad.End

Program.Sub.Error.Start
V.Local.sError.Declare
V.Local.iRet.Declare
V.Local.sSep.Declare

F.Intrinsic.String.Build("-------------------------------------------------------------------------------------",,V.Local.sSep)
F.Intrinsic.String.Build("({5}-{6}){0}{7}{0}Project: {4}{0}{7}{0}Sub: {1}{0}Error: {2}, {3}", V.Ambient.NewLine, V.Args.SubRoutine, V.Args.eNumber, V.Args.eDescription,V.Caller.ScriptFile,V.Ambient.Date, V.Ambient.Time,V.Local.sSep,V.Local.sError)
F.Intrinsic.UI.Msgbox(V.Local.sError,"Error",V.Local.iRet)
Program.Sub.Error.End



